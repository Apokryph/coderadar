buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jacobono:gradle-jaxb-plugin:1.3.6"
    }
}

apply plugin: 'com.github.jacobono.jaxb'

ext {
    generatedSource = 'src/gen/java'
}

dependencies {
    jaxb 'com.sun.xml.bind:jaxb-core:2.2.11'
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.11'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.11'
    jaxb 'javax.xml.bind:jaxb-api:2.2.12-b141001.1542'
    compile project(':plugins:analyzer-plugin-api')
    compile 'com.google.code.findbugs:findbugs:3.0.1'
    compile 'commons-io:commons-io:2.4'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
}

sourceSets.main.java.srcDirs generatedSource

jaxb {
    xsdDir = 'plugins/findbugs-adapter-plugin/src/main/resources/xsd'
    xjc {
        destinationDir = generatedSource
        generatePackage = 'org.wickedsource.coderadar.analyzer.findbugs.xsd'
    }
}

compileJava {
    dependsOn xjc
}

tasks.idea.dependsOn xjc

clean.dependsOn cleanXjc

idea {
    module {
        sourceDirs += file(generatedSource)
        generatedSourceDirs += file(generatedSource)
    }
}
